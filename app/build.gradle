apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.github.chuddington.androidplaygroundmk2"
        minSdkVersion 29
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude "META-INF/LICENSE*"
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

configurations {
    androidTestImplementation.extendsFrom(testImplementation)
    androidTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencies {
    final String espressoVersion = "3.2.0"
    final String jUnit5Version = "5.6.0"
    final String androidJUnit5Version = "1.2.0"


    implementation (
            fileTree(dir: 'libs', include: ['*.jar']),
            "androidx.appcompat:appcompat:1.1.0",
            "androidx.constraintlayout:constraintlayout:1.1.3"
    )

    androidTestImplementation (
            "androidx.test.ext:junit:1.1.1",
            "androidx.test.espresso:espresso-core:${espressoVersion}",
            "androidx.test.espresso:espresso-intents:${espressoVersion}",
            "androidx.test:runner:1.2.0",
            "de.mannodermaus.junit5:android-test-core:${androidJUnit5Version}"
    )

    testImplementation (
            "org.junit.jupiter:junit-jupiter-api:${jUnit5Version}",
            "org.junit.jupiter:junit-jupiter-params:${jUnit5Version}"
    )

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jUnit5Version}"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:${androidJUnit5Version}"
}
